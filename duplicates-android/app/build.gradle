apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = Integer.parseInt(project.APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(project.APP_VERSION_MINOR)

android {
    compileSdkVersion androidBuildSdkVersion

    defaultConfig {
        applicationId "com.github.android.removeduplicates"
        minSdkVersion androidBuildMinSdkVersion
        targetSdkVersion androidBuildTargetSdkVersion
        versionCode versionMajor * 100 + versionMinor
        versionName versionMajor + '.' + versionMinor.toString().padLeft(2, '0')
        resConfigs "en"
        vectorDrawables.useSupportLibrary true

        buildConfigField "Boolean", "FEATURE_CALL_LOGS", "true"
        buildConfigField "Boolean", "FEATURE_SMS", "true"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword project.STORE_PASSWORD_RELEASE
            keyAlias "release"
            keyPassword project.KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        disable "AllowBackup"
        disable "GoogleAppIndexingWarning"
        disable "InconsistentLayout"
        disable "LocaleFolder"
        disable "UnusedAttribute"
        disable "UnusedResources"
    }

    flavorDimensions "privacy"

    productFlavors {
        google {
            dimension "privacy"
            buildConfigField "Boolean", "FEATURE_CALL_LOGS", "false"
            buildConfigField "Boolean", "FEATURE_SMS", "false"
        }
        regular {
            dimension "privacy"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':android-lib:lib')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:1.1.0"

    // Views
    implementation 'androidx.cardview:cardview:1.0.0'

    // Testing
    testImplementation 'junit:junit:4.12'
}

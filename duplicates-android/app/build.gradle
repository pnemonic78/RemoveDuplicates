apply plugin: 'com.android.application'

def versionMajor = Integer.parseInt(project.APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(project.APP_VERSION_MINOR)

android {
    compileSdkVersion androidBuildSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId "com.github.android.removeduplicates"
        minSdkVersion androidBuildMinSdkVersion
        targetSdkVersion androidBuildTargetSdkVersion
        versionCode versionMajor * 100 + versionMinor
        versionName versionMajor + '.' + versionMinor.toString().padLeft(2, '0')
        resConfigs "en"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword project.STORE_PASSWORD_RELEASE
            keyAlias "release"
            keyPassword project.KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        disable "AllowBackup"
        disable "GoogleAppIndexingWarning"
        disable "InconsistentLayout"
        disable "LocaleFolder"
        disable "UnusedAttribute"
        disable "UnusedResources"
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api project(':lib')

    // Views
    api "com.android.support:recyclerview-v7:$supportVersion"
    api "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor group: 'com.jakewharton', name: 'butterknife-compiler', version: "$butterknifeVersion"

    // Testing
    testCompile 'junit:junit:4.12'
}

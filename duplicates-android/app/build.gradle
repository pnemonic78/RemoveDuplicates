buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.30.0'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def versionMajor = Integer.parseInt(project.APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(project.APP_VERSION_MINOR)

android {
    compileSdkVersion androidBuildSdkVersion

    defaultConfig {
        applicationId "com.github.android.removeduplicates"
        minSdkVersion androidBuildMinSdkVersion
        targetSdkVersion androidBuildTargetSdkVersion
        versionCode versionMajor * 100 + versionMinor
        versionName versionMajor + '.' + versionMinor.toString().padLeft(2, '0')
        resConfigs "en"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword project.STORE_PASSWORD_RELEASE
            keyAlias "release"
            keyPassword project.KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable "AllowBackup"
        disable "GoogleAppIndexingWarning"
        disable "InconsistentLayout"
        disable "LocaleFolder"
        disable "UnusedAttribute"
        disable "UnusedResources"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':android-lib:lib')

    // Views
    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor group: 'com.jakewharton', name: 'butterknife-compiler', version: "$butterknife_version"

    // Testing
    testImplementation 'junit:junit:4.12'

    // Logging
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
}
